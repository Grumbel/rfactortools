# Simraceway SRW decrypter (script 0.2.1b)
# script for QuickBMS http://quickbms.aluigi.org

# edit your unique key
# you can insert it in C hex or just hex as below
set MYKEY binary " 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 20 "

strlen TMP MYKEY
if TMP > 32
    string MYKEY h= MYKEY
endif
math USE_MYKEY = 0

idstring "SRW"
getdstring VERSION 5
get KEYNUM long
get SIZE longlong
get FLAGS long
getdstring KEY_SIGN 32  # KEY_SIGN is a data (web key) encrypted with the unique key
if KEY_SIGN !=  ""
if KEY_SIGN !=  "                                "
    if MYKEY == "                                "
        print "Error: the current SimRaceway versions use unique keys for each computer.\n\nYou must take your key with a debugger and writing it in hex or C notation\nin the MYKEY variable of this script.\n\nQuick way to do it by yourself:\nload SimracewayGame.exe in Ollydbg and search the following sequence of\nbytes that are the beginning of the function on which setting the execution\nbreakpoint:\n\n  55 8B EC 8B 55 0C 0F B6 0A\n\nWhen the breakpoint interrupts the execution the key is the sequence of 32\nbytes pointed by Arg2.\nSelect them and then use Edit->\"Binary Copy\" and past the data in one line\nreplacing the sequence of '20's of \"set MYKEY binary\" in this script.\n\n\nOr you can just use the following script:\n\n  http://aluigi.org/papers/bms/others/simraceway_getkey.bms\n\nstart the game and then execute the following command:\n\n  quickbms.exe -p simraceway_getkey.bms process://simraceway/debug\n\n"
        cleanexit
    endif
    math USE_MYKEY = 1
endif
endif

math OFFSET = 0x80
get XSIZE asize
math XSIZE -= OFFSET

# various operations all ending with the same fixed AES keys:
if KEYNUM == 1
    set KEY binary "\x82\xB1\x2E\x82\x25\x36\x99\x84\x96\xA8\xA5\xD9\x50\x7F\x92\x6C\x6F\xA8\x44\x97\x9E\x7B\x58\x19\x94\x91\xB9\x5C\xC1\xE8\xCE\x7B"
elif KEYNUM == 2
    set KEY binary "\xE3\xB0\xC4\x42\x98\xFC\x1C\x14\x9A\xFB\xF4\xC8\x99\x6F\xB9\x24\x27\xAE\x41\xE4\x64\x9B\x93\x4C\xA4\x95\x99\x1B\x78\x52\xB8\x55"
else
    print "unknown key for KEYNUM %KEYNUM%, contact me!"
    cleanexit
endif
if USE_MYKEY != 0
    for i = 0 < 32
        getvarchr TMP MYKEY i
        putvarchr KEY i TMP
    next i
endif

if VERSION == "00001"
    filexor 0xff
    encryption rot KEY "" 1 32
elif VERSION == "00002"
    encryption aes KEY "" 0 32
else
    print "unknown SRW version (%VERSION%), contact me!"
    cleanexit
endif

get NAME basename
get EXT extension
string NAME += "_decrypt."
string NAME += EXT
comtype copy    # for AES padding
clog NAME OFFSET XSIZE SIZE
